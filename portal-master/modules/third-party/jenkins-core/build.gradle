import com.liferay.gradle.plugins.patcher.PatchTask
import com.liferay.gradle.util.FileUtil

apply plugin: "com.liferay.patcher"

task patch(type: PatchTask)
task war(type: Zip)

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

String jenkinsCoreName = "jenkins-core"
String jenkinsCoreVersion = "1.565.1"

dependencies {
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.jenkins-ci.main", name: jenkinsCoreName, optional, version: jenkinsCoreVersion
}

patch {
	ext {
		autoClean = false
	}

	fileNames "hudson/tasks/junit/JUnitResultArchiver.java"
	originalLibModuleName = jenkinsCoreName
	originalLibSrcBaseUrl = "https://repo.jenkins-ci.org/releases/org/jenkins-ci/main"
}

repositories {
	maven {
		url "https://repo.jenkins-ci.org/releases"
	}
}

war {
	baseName = "jenkins-war"

	exclude "META-INF/JENKINS.RSA"
	exclude "WEB-INF/lib/${jenkinsCoreName}-${jenkinsCoreVersion}.jar"

	extension = "war"

	from {
		zipTree(FileUtil.get(project, "https://repo.jenkins-ci.org/releases/org/jenkins-ci/main/jenkins-war/${jenkinsCoreVersion}/jenkins-war-${jenkinsCoreVersion}.war"))
	}

	into("WEB-INF/lib") {
		from jar
	}
}