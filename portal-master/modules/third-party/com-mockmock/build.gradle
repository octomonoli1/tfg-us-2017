import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

task compileJavaMvn(type: Exec)
task downloadSrc(type: Copy)
task jarMvn(type: Exec)

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

String mockMockGitHash = "1e88530f74aea4efa139fec7214f395d25a4032b"
String mockMockSrcUrl = "https://github.com/tweakers-dev/MockMock/archive/${mockMockGitHash}.zip"
File srcDir = new File(buildDir, "MockMock-${mockMockGitHash}")

compileJavaMvn {
	commandLine _getMvnCommand("compile")
	dependsOn downloadSrc
	workingDir = srcDir
}

configurations {
	archives.artifacts.removeAll {
		it.archiveTask in [jarSources, zipJavadoc]
	}
}

downloadSrc {
	from {
		zipTree(FileUtil.get(project, mockMockSrcUrl))
	}

	into buildDir
}

jar {
	deleteAllActions()
	dependsOn jarMvn
}

jarMvn {
	commandLine _getMvnCommand("package")
	dependsOn compileJavaMvn

	doLast {
		copy {
			from new File(srcDir, "target")
			include "MockMock-${project.version}.one-jar.jar"
			into distsDir

			rename {
				jar.archiveName
			}
		}
	}

	workingDir = srcDir
}

private List<String> _getMvnCommand(String command) {
	if (OSDetector.windows) {
		return ["cmd.exe", "/c", "mvn ${command}"]
	}

	return ["/bin/sh", "-c", "mvn ${command}"]
}