import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.OSDetector

task downloadDependencyVisitor(type: Exec)

String dependencyVisitorUrl = "svn://svn.forge.objectweb.org/svnroot/asm/tags/ASM_5_0_1/examples/dependencies/src/org/objectweb/asm/depend/DependencyVisitor.java"

compileJava {
	dependsOn downloadDependencyVisitor
}

dependencies {
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.ant.bnd", version: "2.0.28"
	provided group: "com.liferay", name: "com.liferay.petra.xml", version: "1.0.0"
	provided group: "com.liferay", name: "org.apache.felix.fileinstall", transitive: false, version: "3.5.1-20150728.201454-4-LIFERAY-CACHED"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.util.java", version: "2.0.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.apache.ant", name: "ant", version: "1.9.4"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.ow2.asm", name: "asm-debug-all", version: "5.0.1"
	provided group: "org.springframework", name: "spring-beans", version: "4.1.9.RELEASE"
	provided group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
	provided group: "org.springframework", name: "spring-core", version: "4.1.9.RELEASE"
	provided group: "org.springframework", name: "spring-web", version: "4.1.9.RELEASE"
}

downloadDependencyVisitor {
	File dependencyVisitorDir = file("src/main/java/org/objectweb/asm/depend")

	if (OSDetector.windows) {
		executable "cmd.exe"

		args "/c"
	}
	else {
		executable "/bin/sh"

		args "-c"
	}

	args "\"svn export ${dependencyVisitorUrl} ${dependencyVisitorDir}\""

	doFirst {
		dependencyVisitorDir.mkdirs()
	}

	doLast {
		ant.patch(dir: "src/main/java", patchfile: "patches/LPS-47293.patch", strip: 1)
	}

	onlyIf {
		File dependencyVisitorFile = new File(dependencyVisitorDir, "DependencyVisitor.java")

		!dependencyVisitorFile.exists()
	}

	workingDir = file("src/main/java")
}