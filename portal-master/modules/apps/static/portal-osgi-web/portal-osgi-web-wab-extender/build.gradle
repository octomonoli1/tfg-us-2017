task deployDependencies(type: Copy)

dependencies {
	provided fileTree(dir: "lib-patch", exclude: "*-sources.jar", include: "*.jar")
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	provided group: "com.liferay", name: "com.liferay.osgi.util", version: "3.0.0"
	provided group: "com.liferay", name: "com.liferay.portal.configuration.metatype", version: "2.0.0"
	provided group: "com.liferay", name: "com.liferay.portal.osgi.web.servlet.context.helper", version: "2.1.0"
	provided group: "com.liferay", name: "com.liferay.portal.osgi.web.servlet.jsp.compiler", version: "2.0.0"
	provided group: "com.liferay", name: "com.liferay.portal.profile", version: "1.0.0"
	provided group: "com.liferay", name: "org.apache.axis", version: "1.4.LIFERAY-PATCHED-1"
	provided group: "com.liferay", name: "org.eclipse.osgi", version: "3.10.200-20150904.172142-1-LIFERAY-CACHED"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "commons-fileupload", name: "commons-fileupload", version: "1.2.1"
	provided group: "commons-io", name: "commons-io", version: "1.4"
	provided group: "org.apache.felix", name: "org.apache.felix.utils", version: "1.6.0"
	provided group: "org.glassfish", name: "javax.servlet", version: "3.2-b06"
	provided group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component", version: "1.3.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided group: "org.osgi", name: "org.osgi.service.event", version: "1.3.1"
	provided group: "org.osgi", name: "org.osgi.service.http.whiteboard", version: "1.0.0"
}

deploy {
	finalizedBy deployDependencies
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from(configurations.provided) {
		include "commons-fileupload-*.jar"
		include "commons-io-*.jar"

		String renameSuffix = ".jar"

		if (keepDependencyVersions) {
			renameSuffix = '-$1.jar'
		}

		rename(/commons-fileupload-(.+)\.jar/, "org.apache.commons.fileupload" + renameSuffix)
		rename(/commons-io-(.+)\.jar/, "org.apache.commons.io" + renameSuffix)
	}

	from("lib-patch") {
		exclude "*-sources.jar"
		include "org.eclipse.equinox.http.servlet-*.jar"

		if (!keepDependencyVersions) {
			rename {
				"org.eclipse.equinox.http.servlet.jar"
			}
		}
	}

	into {
		liferay.deployDir
	}
}