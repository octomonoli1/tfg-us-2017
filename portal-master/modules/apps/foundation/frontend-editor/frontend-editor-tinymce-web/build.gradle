import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildTinyMCE(type: Copy)

String tinyMCEVersion = "4.3.12"

File editorDestinationDir = file("tmp/META-INF/resources")
File editorSrcDir = file("src/main/resources/META-INF/resources")
File thirdPartyDir = file("third-party")

buildTinyMCE {
	File tinyMCEDestinationDir = new File(editorDestinationDir, "tiny_mce")
	File tinyMCEZipFile = new File(thirdPartyDir, "tinymce_${tinyMCEVersion}.zip")

	doFirst {
		delete tinyMCEDestinationDir
	}

	duplicatesStrategy = "exclude"

	from new File(editorSrcDir, "_diffs")

	from(zipTree(tinyMCEZipFile)) {
		eachFile new StripPathSegmentsAction(3)
		exclude "**/moxieplayer.swf"
		include "tinymce/js/tinymce/"
	}

	includeEmptyDirs = false
	into tinyMCEDestinationDir

	onlyIf {
		!FileUtil.isUpToDate(project, tinyMCEZipFile, tinyMCEDestinationDir)
	}
}

classes {
	dependsOn buildTinyMCE
}

dependencies {
	provided group: "com.liferay", name: "com.liferay.frontend.editor.lang", version: "1.0.0"
	provided group: "com.liferay", name: "com.liferay.frontend.taglib", version: "2.0.0"
	provided group: "com.liferay", name: "com.liferay.registry.api", version: "1.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	provided group: "javax.portlet", name: "portlet-api", version: "2.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
}