import com.liferay.gradle.util.copy.StripPathSegmentsAction

apply plugin: "com.liferay.cache"

configurations {
	metalComponents
}

task buildMetalComponents(type: Copy)

String metalComponentsVersion = "1.0.0-rc.28"

buildMetalComponents {
	eachFile new StripPathSegmentsAction(7)

	from {
		zipTree(configurations.metalComponents.singleFile)
	}

	include "META-INF/resources/webjars/metal-components/${metalComponentsVersion}/build/amd-jquery/"
	include "META-INF/resources/webjars/metal-components/${metalComponentsVersion}/build/soy/"

	includeEmptyDirs = false
	into "classes/META-INF/resources"
}

cache {
	task(classes) {
		baseDir = "classes/META-INF"
		cacheDir = ".task-cache"
		skipTaskDependency configJSModules

		testFile "build.gradle"
		testFile "package.json"
	}
}

configJSModules {
	dependsOn buildMetalComponents

	include "**/*.js*"
}

dependencies {
	metalComponents group: "com.liferay.webjars", name: "com.liferay.webjars.metal-components", transitive: false, version: metalComponentsVersion
}