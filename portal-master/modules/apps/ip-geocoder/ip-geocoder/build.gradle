task deployDependencies(type: Copy)

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

dependencies {
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.portal.configuration.metatype", version: "2.0.0"
	provided group: "com.maxmind.geoip", name: "geoip-api", version: "1.2.14"
	provided group: "log4j", name: "log4j", version: "1.2.17"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided group: "org.tukaani", name: "xz", version: "1.5"
}

deploy {
	finalizedBy deployDependencies
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from configurations.provided

	include "geoip-api-*.jar"
	include "xz-*.jar"

	into {
		liferay.deployDir
	}

	String renameSuffix = ".jar"

	if (keepDependencyVersions) {
		renameSuffix = '-$1.jar'
	}

	rename(/geoip-api-(.+)\.jar/, "com.maxmind.geoip.api" + renameSuffix)
	rename(/xz-(.+)\.jar/, "org.tukaani.xz" + renameSuffix)
}